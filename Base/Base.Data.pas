{-----------------------------------------------------------------------------------------------------------------------
  Project:     Galahad
  Unit:        Base.Data
  Author:      David Harper
  License:     MIT
  History:     2026-08-02 Initial version 0.1
  Purpose:     Provides simple data abstractions.
-----------------------------------------------------------------------------------------------------------------------}

unit Base.Data;

interface

uses
  System.SysUtils,
  System.Classes,
  System.Rtti,
  System.TypInfo,
  System.Variants,
  System.Generics.Collections,
  FireDAC.Comp.Client,
  FireDAC.Stan.Param,
  Base.Core,
  Base.Files,
  Base.Integrity,
  Base.Dynamic,
  Base.Collections,
  Base.Settings;

type
  IDbContextFingerprint = interface
    ['{A16A68A9-A17F-4975-B975-597C28DBB3F8}']
    function Fingerprint: string;
  end;

  IDbContext = interface
    ['{5A8FA1CA-DF84-4371-935A-2E0385F3CE04}']
    function ProviderId: string;   // e.g. 'sqlite'
    function Name: string;         // e.g. 'default'
    function Payload: IInterface;  // provider-specific, strongly-typed via interface cast
  end;

  IDbContextProvider = interface
    ['{7D9A8D2C-8E7D-4C11-9F4E-7C6D7E4B9A10}']
    function ProviderId: string; // e.g. 'sqlite'
    function BuildContext(const aFileService: IFileService; const Settings: ISettings): IDbContext;
  end;

  IDbContextFactory = interface
    ['{B9A7D1A2-2D5B-4A92-8E1E-0C4B1C6D9A33}']
    function BuildFromSettings(const Settings: ISettings): IDbContext;
  end;

  TDbContextFactory = class(TSingleton, IDbContextFactory)
  private
    fFileService: IFileService;
  public
    function BuildFromSettings(const Settings: ISettings): IDbContext;
    constructor Create(const aFileService: IFileService);
  end;

  IDbSession = interface
    ['{FC69B63A-0EA7-4C27-9641-202F66B2FE4A}']
    function Connection: TFDConnection;
    function NewQuery: TFDQuery;

    procedure StartTransaction;
    procedure Commit;
    procedure Rollback;

    // Transitional: schema/user version for migrations
    function GetSchemaVersion: Integer;
    procedure SetSchemaVersion(const Value: Integer);
  end;

  IDbSessionFactory = interface
    ['{A152B6F7-B167-40C1-B70E-083182CDD68D}']
    function OpenSession(const aCtx: IDbContext): IDbSession;
  end;

  IDbSessionManager = interface
    ['{7C695785-EA4A-452F-9B89-67198A4CB873}']
    // Install context for the *current thread* (important for Tasks). Guard restores previous state.
    function UseContext(const aCtx: IDbContext): IInterface;

    // Returns the "current" session:
    // - active transactional session if inside InTransaction
    // - otherwise per-thread cached session (lazy)
    function CurrentSession: IDbSession;

    // Runs Proc inside a transaction on the current thread.
    procedure InTransaction(const aProc: TProc);

    // Optional: clear per-thread cached session (useful for tests or context changes)
    procedure ClearThreadSession;
  end;

  IDbStartupHook = interface
    ['{2C01D581-ED3F-4814-94F1-E14F6FF561BA}']
    procedure Execute(const aDb: IDbSessionManager; const aCtx: IDbContext);
  end;

  TDbSessionManager = class(TSingleton, IDbSessionManager)
  private
    fFactory: IDbSessionFactory;

    class threadvar tCtx: IDbContext;
    class threadvar tActive: IDbSession;

    // per-thread cached session (lazy)
    class threadvar tThreadSession: IDbSession;
    class threadvar tThreadSessionFingerprint: string;

    function Fingerprint(const aCtx: IDbContext): string;
    function EnsureThreadSession(const aCtx: IDbContext): IDbSession;
  public
    constructor Create(const aFactory: IDbSessionFactory);

    function UseContext(const aCtx: IDbContext): IInterface;
    function CurrentSession: IDbSession;

    procedure InTransaction(const aProc: TProc);
    procedure ClearThreadSession;
  end;

  /// <summary>
  ///  Task/threadpool threads do not inherit the ambient DB context (it is stored in TLS).
  ///  Therefore, every TTask must install the context explicitly and keep the guard alive
  ///  for the full duration of the task body.
  ///
  ///  Pattern:
  ///
  ///   TTask.Run(
  ///     procedure
  ///     var
  ///       G: IInterface;
  ///       DbMgr: IDbSessionManager;
  ///     begin
  ///       DbMgr := Container.Resolve<IDbSessionManager>;  // or capture it
  ///       G := DbMgr.UseContext(Ctx);                     // Ctx is immutable, safe to capture
  ///
  ///       // do DB work here (repos will resolve DbMgr.CurrentSession)
  ///     end
  ///   );
  ///
  ///  Do not store G anywhere global; it must be released on the same thread that installed it.
  ///  </summary>
  TDbAmbientGuard = class(TTransient)
  private
    fPrevCtx: IDbContext;
    fPrevActive: IDbSession;
  public
    constructor Create(const aPrevCtx: IDbContext; const aPrevActive: IDbSession);
    destructor Destroy; override;
  end;

  IDbAmbientInstaller = interface
    ['{11C84219-66C7-4096-A54D-744546298759}']
  end;

  TDbAmbientInstaller = class(TSingleton, IDbAmbientInstaller)
  private
    fGuard: IInterface;
  public
    constructor Create(const aDbMgr: IDbSessionManager; const aCtx: IDbContext);
    destructor Destroy; override;
  end;

  {---------------------------------------------------------------------------------------------------------------------
    Helpers for converting Specifications to SQL Where clauses.
  ---------------------------------------------------------------------------------------------------------------------}

  TSqlParam = record
    Name: string;
    Value: Variant;
  end;

  TSqlWhere = record
    Sql: string;
    Params: TArray<TSqlParam>;
  end;

  ISqlBuildContext = interface
    ['{F6D0C2A6-7C4B-4F83-A8B0-3B33F2A8B7C1}']
    function AddParam(const aValue: Variant): string;
    function Alias: string;
    function Column(const aName: string): string;
  end;

  ISpecSqlAdapter<T> = interface
    ['{2C3A9D5D-1D0B-4A4E-B5EA-7E0B4B5A2B3A}']
    function TryBuildWhere(const aSpec: ISpecification<T>; const aCtx: ISqlBuildContext; out aSql: string): Boolean;
  end;

  ESpecNotTranslatable = class(Exception);

  TSqlBuildContext = class(TInterfacedObject, ISqlBuildContext)
  private
    fAlias: string;
    fNext: Integer;
    fParams: TList<TSqlParam>;
  public
    constructor Create(const aAlias: string);
    destructor Destroy; override;

    function AddParam(const aValue: Variant): string;
    function Alias: string;
    function Column(const aName: string): string;

    function DetachParams: TArray<TSqlParam>;
  end;

  TSpecSqlBuilder<T> = class
  private
    fAdapters: TDictionary<TClass, ISpecSqlAdapter<T>>;
    fAlias: string;

    function BuildInternal(const aSpec: ISpecification<T>; const aCtx: TSqlBuildContext): string;
    function TryFindAdapter(const aSpec: ISpecification<T>; out aAdapter: ISpecSqlAdapter<T>): Boolean;
  public
    constructor Create(const aAlias: string = '');
    destructor Destroy; override;

    procedure RegisterAdapter(const aSpecClass: TClass; const aAdapter: ISpecSqlAdapter<T>);

    function BuildWhere(const aSpec: ISpecification<T>): TSqlWhere;
  end;

  {---------------------------------------------------------------------------------------------------------------------
    Entity and DbSet abstractions for lightweight mapping of classes and properties to tables and columns.
    This initial version is intentionally minimal and focused on the common path.
    The goal is a simple, extensible data abstraction rather than a fully featured ORM.
  ---------------------------------------------------------------------------------------------------------------------}

  /// <summary>
  ///  Apply to non-persisted entity fields.
  /// </summary>
  TransientAttribute = class(TCustomAttribute) end;

  /// <summary>
  ///  Specifies the name of the table an entity maps to.
  ///  If not specified, the name of the entity is used - minus the first T if present.
  /// </summary>
  TTableAttribute = class(TCustomAttribute)
  private
    fName: string;
  public
    property Name: string read fName write fName;

    constructor Create(const aName: string);
  end;

  /// <summary>
  ///  Specifies the name of a column an entity property maps to.
  ///  If not specified, the name of the property is used.
  /// <summary>
  TColumnAttribute = class(TCustomAttribute)
  private
    fName: string;
  public
    property Name: string read fName write fName;

    constructor Create(const aName: string);
  end;

  IEntity = interface
    ['{DFEF00C0-5695-443E-91A7-E79E794ED794}']

    /// <summary>Sets the Id.</summary>
    procedure SetId(const aId: integer);

    /// <summary>Gets the Id.</summary>
    function GetId: integer;

    /// <summary>Returns true if the entity does not have an Id.</summary>
    function IsNew: boolean;

    /// <summary>Returns true if the entity does have an Id.</summary>
    function Exists: boolean;

    property Id: integer read GetId write SetId;
  end;

  TEntity = class(TInterfacedObject, IEntity)
  private
    fId: integer;
  public
    procedure SetId(const aId: integer);

    function GetId: integer;
    function IsNew: boolean;
    function Exists: boolean;

    property Id: integer read GetId write SetId;
  end;

  IDbSet<TService: IEntity; T: TEntity, constructor> = interface
    ['{2B0A8B8E-2A59-43E7-8B3F-0A6B8A2A4A3C}']

    function TableName: string;

    function GetAll: TArray<TService>;
    function GetBy(const aId: integer): TOption<TService>;
  end;

{$IFDEF MSWINDOWS}
  TDbSet<TService: IEntity; T: TEntity, constructor> = class(TDynamicObject, IDbSet<TService, T>)
{$ELSE}
  TDbSet<TService: IEntity; T: TEntity, constructor> = class(TTransient, IDbSet<TService, T>)
{$ENDIF}
  private
    fDb: IDbSessionManager;

    function GetQueryResults(const aQuery: TFDQuery):TList<TService>;
  protected
    function Database: IDbSessionManager;
    function Connection: TFDConnection;
    function NewQuery: TFDQuery;

    function ExecQuery(const aSql: string):TList<TService>; overload;
    function ExecQuery(const aQuery: TFDQuery):TList<TService>; overload;

    class var fName: string;
    class var fPropertyOrder: TList<string>;
    class var fProperties: TDictionary<string, TRttiProperty>;
    class var fColToPropMap: TDictionary<string, string>;
    class var fPropToColMap: TDictionary<string, string>;
  public
    function TableName: string;

    function GetAll: TArray<TService>;
    function GetBy(const aId: integer): TOption<TService>;

    constructor Create(const aDb: IDbSessionManager);
    destructor Destroy; override;

    class constructor Create;
    class destructor Destroy;
  end;

  TMigration = class
  private
    fVersion: integer;
    fSequence: integer;
    fDescription: string;
  public
    property Version: integer read fVersion write fVersion;
    property Sequence: integer read fSequence write fSequence;
    property Description: string read fDescription write fDescription;

    procedure Execute(const aDb: IDbSessionManager); virtual;
  end;

  TMigrationClass = class of TMigration;

  IMigrationManager = interface
    ['{902A60CA-F419-4CEC-A4D3-7A1C1A2D37AA}']
    procedure Add(const aVersion: integer; const aSequence: integer; const aMigration:TMigrationClass; const aDescription: string);
    procedure Execute;
  end;

  IMigrationRegistrar = interface
    ['{C64AD729-DDB8-4AF0-9D23-92BE9E830E14}']
    procedure Configure(const m: IMigrationManager);
  end;

  TMigrationManager = class(TTransient, IMigrationManager)
  private
    fMigrations: TObjectList<TMigration>;
    fDb: IDbSessionManager;
  public
    procedure Add(const aVersion: integer; const aSequence: integer; const aMigration:TMigrationClass; const aDescription: string);
    procedure Execute;

    constructor Create(const aDb: IDbSessionManager; const aRegistrar: IMigrationRegistrar);
    destructor Destroy; override;
  end;

implementation

uses
  System.Math,
  System.Generics.Defaults,
  Base.Reflection,
  Base.Container;

{ TTableAttribute }

{----------------------------------------------------------------------------------------------------------------------}
constructor TTableAttribute.Create(const aName: string);
begin
  fName := aName;
end;

{ TColumnAttribute }

{----------------------------------------------------------------------------------------------------------------------}
constructor TColumnAttribute.Create(const aName: string);
begin
  fName := aName;
end;

{ TEntity }

{----------------------------------------------------------------------------------------------------------------------}
function TEntity.GetId: integer;
begin
  Result := fId;
end;

{----------------------------------------------------------------------------------------------------------------------}
procedure TEntity.SetId(const aId: integer);
begin
  fId := aId;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TEntity.Exists: boolean;
begin
  Result := fId > 0;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TEntity.IsNew: boolean;
begin
  Result := fId < 1;
end;

{ TDbSet<TService, T> }

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.Database: IDbSessionManager;
begin
  Result := fDb;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.Connection: TFDConnection;
begin
  Result := fDb.CurrentSession.Connection;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.NewQuery: TFDQuery;
begin
  Result := fDb.CurrentSession.NewQuery;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.GetAll: TArray<TService>;
const
  SQL = 'select * from %s';
var
  scope: TScope;
begin
  var qry := SQL.Format(SQL, [TableName]);
  var list := scope.Owns(ExecQuery(qry));

  Result := list.ToArray;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.GetBy(const aId: integer): TOption<TService>;
const
  SQL = 'select * from %s where id = %d';
var
  scope: TScope;
begin
  var qry := SQL.Format(SQL, [TableName, aId]);

  var list := scope.Owns(ExecQuery(qry));

  if list.IsEmpty then
    Result.SetNone
  else
    Result.SetSome(list[0]);
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.TableName: string;
begin
  Result := fName;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.ExecQuery(const aSql: string):TList<TService>;
begin
  var query := NewQuery;

  try
    query.SQL.Text := aSQL;
    Result := ExecQuery(query);
  finally
    query.Free;
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.ExecQuery(const aQuery: TFDQuery): TList<TService>;
begin
  aQuery.Open;
  try
    Result := GetQueryResults(aQuery);
  finally
    aQuery.Close;
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSet<TService, T>.GetQueryResults(const aQuery: TFDQuery): TList<TService>;
const
  ERR = '%s does not support requested interface %s';
var
  lValue: TValue;
  lVariant: Variant;

  scope: TScope;
begin
  var entities := TList<TService>.Create;

  if aQuery.RecordCount = 0 then exit(entities);

  scope.Owns(entities);

  aQuery.First;

  while not aQuery.Eof do
  begin
    var entity := T.Create;

    for var nameProperty in fProperties do
    begin
      var name := nameProperty.Key;
      var prop := nameProperty.Value;
      var col  := fPropToColMap[name];

      lVariant := aQuery[col];

      if not TReflection.TryVariantToTValue(lVariant, prop.PropertyType.Handle, lValue) then
        lValue := TValue.FromVariant(lVariant);

      prop.SetValue(TObject(entity), lValue);
    end;

    entities.Add(TReflection.As<TService>(entity));

    aQuery.Next;
  end;

  Result := scope.Release(entities);
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TDbSet<TService, T>.Create(const aDb: IDbSessionManager);
begin
  inherited Create;

  fDb := aDb;
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TDbSet<TService, T>.Destroy;
begin
  inherited;
end;

{----------------------------------------------------------------------------------------------------------------------}
class constructor TDbSet<TService, T>.Create;
const
  TYPE_ERR   = '%s does not implement %s';
  IFCE_COUNT = 'RefCount';
var
  lCtx: TRttiContext;
begin
{$IFDEF DEBUG}
  // Delphi can't constrain Class + Interface on T, so we fail fast here: T must implement TService.
  Assert(TReflection.Is<T, TService>, Format(TYPE_ERR, [T.ClassName, TReflection.TypeNameOf<TService>]));
{$ENDIF}

  lCtx := TRttiContext.Create;

  var lType :=  lCtx.GetType(T);
  var lClass := TRttiInstanceType(lType);

  var tableAttr := lType.GetAttribute<TTableAttribute>;

  if Assigned(tableAttr) then
    fName := tableAttr.Name
  else
  begin
    if lClass.Name.StartsWith('T') then
      fName := lClass.Name.Substring(1)
    else
      fName := lClass.Name;
  end;

  fPropertyOrder := TList<string>.Create;
  fProperties    := TDictionary<string, TRttiProperty>.Create(TIStringComparer.Ordinal);
  fColToPropMap  := TDictionary<string, string>.Create(TIStringComparer.Ordinal);
  fPropToColMap  := TDictionary<string, string>.Create(TIStringComparer.Ordinal);

  for var lProperty in lType.GetProperties do
  begin
    if SameText(lProperty.Name, IFCE_COUNT) then Continue;

    if Assigned(lProperty.GetAttribute<TransientAttribute>()) then continue;

    var colAttr := lProperty.GetAttribute<TColumnAttribute>;

    if Assigned(colAttr) then
    begin
      fColToPropMap.Add(colAttr.Name, lProperty.Name);
      fPropToColMap.Add(lProperty.Name, colAttr.Name);
    end
    else
    begin
      fColToPropMap.Add(lProperty.Name, lProperty.Name);
      fPropToColMap.Add(lProperty.Name, lProperty.Name);
    end;

    fProperties.Add(lProperty.Name, lProperty);
    fPropertyOrder.Add(lProperty.Name);
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
class destructor TDbSet<TService, T>.Destroy;
begin
  FreeAndNil(fProperties);
  FreeAndNil(fColToPropMap);
  FreeAndNil(fPropertyOrder);
end;

{ TMigration }

{----------------------------------------------------------------------------------------------------------------------}
procedure TMigration.Execute(const aDb: IDbSessionManager);
const
  MSG = 'Applying migration (%d.%d): %s';
begin
  Writeln(Format(MSG, [fVersion, fSequence, fDescription]));
end;

{ TMigrationManager }

{----------------------------------------------------------------------------------------------------------------------}
procedure TMigrationManager.Execute;
const
  ERR_DETAIL  = 'Migration Error (%d.%d - %s): %s';
  ERR_SUMMARY = 'Migration Error (%d): %s';
var
  scope: TScope;
begin
  var version := fDb.CurrentSession.GetSchemaVersion;
  var max := 0;

  for var m in fMigrations do
    if m.Version > max then
      max := m.Version;

  if max = version then exit;

  var migrations := Stream
    .From<TMigration>(fMigrations.ToArray)
    .Filter(
        function(const m: TMigration): Boolean
        begin
          Result := m.Version > version;
        end)
    .Sort(TComparer<TMigration>.Construct(
        function(const l, r: TMigration): integer
        begin
          if l.Version <> r.Version then
            Result := Ord(CompareValue(l.Version, r.Version))
          else
            Result := Ord(CompareValue(l.Sequence, r.Sequence));
        end))
    .GroupBy<integer>(
        function(const m: TMigration): integer
        begin
          Result := m.Version;
        end);

  scope.Owns(migrations);
  scope.Defer(procedure begin for var item in migrations do item.Value.Free; end);

  Inc(version);

  for var v in [version..max] do
  begin
    var m: TMigration := nil;

    try
      fDb.CurrentSession.StartTransaction;

      for m in migrations[v] do
        m.Execute(fDb);

      fDb.CurrentSession.SetSchemaVersion(v);
      fDb.CurrentSession.Commit;
    except
      on E:Exception do
      begin
        fDb.CurrentSession.Rollback;

        var msg := if Assigned(m) then
                     Format(ERR_DETAIL, [v, m.Sequence, m.Description, E.Message])
                   else
                     Format(ERR_SUMMARY, [v, E.Message]);

        raise Exception.Create(msg);
      end;
    end;
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
procedure TMigrationManager.Add(const aVersion: integer; const aSequence: integer; const aMigration:TMigrationClass; const aDescription: string);
begin
  var m := aMigration.Create;

  m.Version     := aVersion;
  m.Sequence    := aSequence;
  m.Description := aDescription;

  fMigrations.Add(m)
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TMigrationManager.Create(const aDb: IDbSessionManager; const aRegistrar: IMigrationRegistrar);
begin
  fDb := aDb;
  fMigrations := TObjectList<TMigration>.Create(true);

  aRegistrar.Configure(Self);
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TMigrationManager.Destroy;
begin
  fMigrations.Free;
  inherited;
end;

{ TDbAmbientGuard }

{----------------------------------------------------------------------------------------------------------------------}
constructor TDbAmbientGuard.Create(const aPrevCtx: IDbContext; const aPrevActive: IDbSession);
begin
  inherited Create;

  fPrevCtx := aPrevCtx;
  fPrevActive := aPrevActive;
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TDbAmbientGuard.Destroy;
begin
  TDbSessionManager.tActive := fPrevActive;
  TDbSessionManager.tCtx := fPrevCtx;

  inherited;
end;

{ TDbSessionManager }

{----------------------------------------------------------------------------------------------------------------------}
procedure TDbSessionManager.ClearThreadSession;
begin
  tThreadSession := nil;
  tThreadSessionFingerprint := '';
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSessionManager.CurrentSession: IDbSession;
begin
  // If inside an explicit transaction, always return that session.
  if tActive <> nil then exit(tActive);

  if tCtx = nil then
    raise Exception.Create('No ambient DB context set for this thread. Call UseContext(...) first.');

  Result := EnsureThreadSession(tCtx);
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSessionManager.EnsureThreadSession(const aCtx: IDbContext): IDbSession;
begin
  var fp := Fingerprint(aCtx);

  if (tThreadSession = nil) or (tThreadSessionFingerprint <> fp) then
  begin
    tThreadSession := fFactory.OpenSession(aCtx);
    tThreadSessionFingerprint := fp;
  end;

  Result := tThreadSession;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSessionManager.Fingerprint(const aCtx: IDbContext): string;
var
  FP: IDbContextFingerprint;
begin
  if aCtx = nil then
    raise EArgumentNilException.Create('aCtx');

  Result := aCtx.ProviderId + '|' + aCtx.Name;

  if Supports(aCtx.Payload, IDbContextFingerprint, FP) then
    Result := Result + '|' + FP.Fingerprint
  else
    // Fallback: if no fingerprint is provided, treat payload changes as "unknown"
    // and avoid caching by returning something that will not match across calls.
    Result := Result + '|nocache';
end;

{----------------------------------------------------------------------------------------------------------------------}
procedure TDbSessionManager.InTransaction(const aProc: TProc);
begin
  if not Assigned(aProc) then
    raise EArgumentNilException.Create('aProc');

  if tCtx = nil then
    raise Exception.Create('No ambient DB context set for this thread. Call UseContext(...) first.');

  // Use the per-thread session. (Simple, predictable for v0.1.)
  var session := EnsureThreadSession(tCtx);

  var prevActive := tActive;

  tActive := session;
  try
    session.StartTransaction;
    try
      aProc();
      session.Commit;
    except
      session.Rollback;
      raise;
    end;
  finally
    tActive := prevActive;
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TDbSessionManager.UseContext(const aCtx: IDbContext): IInterface;
var
  PrevCtx: IDbContext;
  PrevActive: IDbSession;
begin
  if aCtx = nil then
    raise EArgumentNilException.Create('aCtx');

  PrevCtx := tCtx;
  PrevActive := tActive;

  tCtx := aCtx;

  // Note: do NOT create the session here; keep it lazy.

  Result := TDbAmbientGuard.Create(PrevCtx, PrevActive);
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TDbSessionManager.Create(const aFactory: IDbSessionFactory);
begin
  inherited Create;

  fFactory := aFactory;
end;

{ TDbAmbientInstaller }

{----------------------------------------------------------------------------------------------------------------------}
constructor TDbAmbientInstaller.Create(const aDbMgr: IDbSessionManager; const aCtx: IDbContext);
begin
  inherited Create;

  fGuard := aDbMgr.UseContext(aCtx);
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TDbAmbientInstaller.Destroy;
begin
  fGuard := nil;

  inherited;
end;

{ TDbContextFactory }

{----------------------------------------------------------------------------------------------------------------------}
function TDbContextFactory.BuildFromSettings(const Settings: ISettings): IDbContext;
begin
  Ensure.IsTrue(Assigned(Settings), 'Settings is required');

  var database   := Settings.Database;
  var providerId := database.Attr('provider', 'sqlite').AsString;
  var provider   := Container.Resolve<IDbContextProvider>(providerId);

  Ensure.IsTrue(Assigned(provider), 'Database provider not registerd: ' + providerId);

  Result := Provider.BuildContext(fFileService, Settings);
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TDbContextFactory.Create(const aFileService: IFileService);
begin
  fFileService := aFileService;
end;

{ TSqlBuildContext }

{----------------------------------------------------------------------------------------------------------------------}
function TSqlBuildContext.Column(const aName: string): string;
begin
  if Length(fAlias) = 0 then exit(aName);

  Result := fAlias + '.' + aName;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSqlBuildContext.Alias: string;
begin
  Result := FAlias;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSqlBuildContext.AddParam(const aValue: Variant): string;
var
  p: TSqlParam;
begin
  Result := ':p' + fNext.ToString;

  Inc(fNext);

  p.Name  := Result;
  p.Value := aValue;

  fParams.Add(p);
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSqlBuildContext.DetachParams: TArray<TSqlParam>;
begin
  Result := fParams.ToArray;

  fParams.Clear;
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TSqlBuildContext.Create(const aAlias: string);
begin
  inherited Create;

  fAlias  := aAlias;
  fNext   := 0;
  fParams := TList<TSqlParam>.Create;
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TSqlBuildContext.Destroy;
begin
  fParams.Free;

  inherited;
end;

{ TSpecSqlBuilder<T> }

{----------------------------------------------------------------------------------------------------------------------}
procedure TSpecSqlBuilder<T>.RegisterAdapter(const aSpecClass: TClass; const aAdapter: ISpecSqlAdapter<T>);
begin
  Ensure.IsTrue(Assigned(aSpecClass), 'SpecClass is nil')
        .IsTrue(Assigned(aAdapter), 'Adapter is nil');

  FAdapters.AddOrSetValue(aSpecClass, aAdapter);
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSpecSqlBuilder<T>.TryFindAdapter(const aSpec: ISpecification<T>; out aAdapter: ISpecSqlAdapter<T>): Boolean;
begin
  aAdapter := nil;

  if aSpec = nil then exit(false);

  Result := FAdapters.TryGetValue((aSpec as TObject).ClassType, aAdapter);
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSpecSqlBuilder<T>.BuildInternal(const aSpec: ISpecification<T>; const aCtx: TSqlBuildContext): string;
const
  NO_ADAPTER = 'No SQL adapter registered for specification %s';
  NO_ADAPT   = 'Specification %s is not translatable by its adapter';
var
  andSpec: IAndSpecification<T>;
  orSpec: IOrSpecification<T>;
  notSpec: INotSpecification<T>;
  adapter: ISpecSqlAdapter<T>;
  leafSql: string;
begin
  Ensure.IsTrue(Assigned(aSpec), 'Spec is nil');

  // Composite nodes
  if Supports(aSpec, IAndSpecification<T>, andSpec) then
    Exit('(' + BuildInternal(andSpec.Left, aCtx) + ' AND ' + BuildInternal(andSpec.Right, aCtx) + ')');

  if Supports(aSpec, IOrSpecification<T>, orSpec) then
    Exit('(' + BuildInternal(orSpec.Left, aCtx) + ' OR ' + BuildInternal(orSpec.Right, aCtx) + ')');

  if Supports(aSpec, INotSpecification<T>, notSpec) then
    Exit('(NOT ' + BuildInternal(notSpec.Inner, aCtx) + ')');

  // Leaf
  if not TryFindAdapter(aSpec, adapter) then
    raise ESpecNotTranslatable.CreateFmt(NO_ADAPTER, [(aSpec as TObject).ClassName]);

  if not adapter.TryBuildWhere(aSpec, aCtx, leafSql) then
    raise ESpecNotTranslatable.CreateFmt(NO_ADAPT, [(aSpec as TObject).ClassName]);

  Result := leafSql;
end;

{----------------------------------------------------------------------------------------------------------------------}
function TSpecSqlBuilder<T>.BuildWhere(const aSpec: ISpecification<T>): TSqlWhere;
var
  ctx: TSqlBuildContext;
begin
  Ensure.IsTrue(Assigned(aSpec), 'Spec is nil');

  ctx := TSqlBuildContext.Create(FAlias);
  try
    Result.Sql    := BuildInternal(aSpec, ctx);
    Result.Params := ctx.DetachParams;
  finally
    ctx.Free;
  end;
end;

{----------------------------------------------------------------------------------------------------------------------}
constructor TSpecSqlBuilder<T>.Create(const aAlias: string);
begin
  inherited Create;

  fAlias    := aAlias;
  fAdapters := TDictionary<TClass, ISpecSqlAdapter<T>>.Create;
end;

{----------------------------------------------------------------------------------------------------------------------}
destructor TSpecSqlBuilder<T>.Destroy;
begin
  FAdapters.Free;

  inherited;
end;

end.
